// Parsing Expression Grammar for RiveScript
// Based on RiveScript 2.00 Working Draft (2013/06/13)
// https://www.rivescript.com/wd/RiveScript

file      = { SOI ~ (statement | blank_line)+ ~ EOI }
statement = { (block | definition) }
block     = { (begin_block | topic_block | object_macro | trigger_response_block) }

// Atoms:
WHITESPACE = _{ " " }
COMMENT    = _{ ("/*" ~ (!"*/" ~ ANY)* ~ "*/") | ("//" ~ (!NEWLINE ~ ANY)*) }
continue   = _{ NEWLINE ~ "^" }
blank_line = _{ NEWLINE }
EOL        = _{ !continue ~ NEWLINE }
special    =  { ("{" | "<" | "}" | ">" | "//" | "/*") }
txt        = _{ !(EOL | special) ~ ASCII }

// Defintions:
definition          =  { "!" ~ (variable_definition | version_declaration) }
inline_definition   = @{ "{" ~ definition ~ "}" }
var_type            =  { ("global" | "var" | "array" | "sub" | "person") }
var_name            =  { (ASCII_ALPHA | "_")+ }
value               =  { txt+ }
version_number      =  { ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+){, 2} }
version_declaration =  { "version" ~ "=" ~ version_number }
variable_definition =  { var_type ~ var_name ~ "=" ~ value }

// Label blocks...
begin_block  =  { ">" ~ "begin" ~ body ~ "<" ~ "begin" }
topic_block  =  { ">" ~ "topic" ~ topic_name ~ body ~ "<" ~ "topic" }
object_start =  { ">" ~ "object" ~ object_name ~ object_lang }
object_end   = _{ "< object" }
object_body  =  { (!object_end ~ ANY)* }
object_macro =  { object_start ~ object_body ~ object_end }
body         =  { !"<" ~ trigger_response_block* }
object_name  = @{ (!WHITESPACE ~ txt)+ }
object_lang  = @{ (!WHITESPACE ~ txt)+ }
topic_name   = @{ !WHITESPACE ~ txt+ }

// Triggers & responses...
trigger_response_block = { trigger ~ continue? ~ (response)+ }

trigger_txt         = _{ !(EOL | special) ~ (ASCII_ALPHA_LOWER | ASCII_DIGIT)+ }
optional            =  { "[" ~ value ~ "]" }
array_ref           = ${ "@" ~ txt }
inline_alternatives =  { trigger_txt ~ ("|" ~ trigger_txt)* }
trigger_alternation =  { "(" ~ (array_ref | inline_alternatives) ~ ")" }
wildcard            =  { ("*" | "_" | "#") }
trigger_content     =  { (trigger_txt | trigger_alternation | optional | wildcard) }
trigger             =  { "+" ~ trigger_content+ ~ EOL }

response_content = { (txt | tag)+ }
response         = { "-" ~ response_content ~ weight? ~ EOL }

previous = { "%" ~ response_content ~ EOL }

operator    = { ("==" | "eq" | "!=" | "ne" | "<>" | "<=" | ">=" | "<" | ">") }
conditional = { "*" ~ (tag | value) ~ operator ~ (tag | value) }

// Tags...
format_tag_name =  { ("random" | "person" | "formal" | "sentence" | "uppercase" | "lowercase") }
numbered_names  = _{ ("botstar" | "star" | "input") ~ ASCII_NONZERO_DIGIT? }

// Single tags

variable_ref_tag   = { "<" ~ ("bot" | "env") ~ var_name ~ ">" }
response_tag_names = { (numbered_names | "id" | "@") }
response_tag       = { "<" ~ (paired_tag_names | response_tag_names) }
operation          = { ("get" | "set" | "add" | "sub" | "mult" | "div") }
variable_set_tag   = { "<" ~ operation ~ var_name ~ "=" ~ (tag | value) ~ ">" }
single_tag         = { (response_tag | variable_set_tag | variable_ref_tag) }

// Paired tags
paired_tag_names = { ("random" | "person" | "formal" | "sentence" | "uppercase" | "lowercase") }
paired_tag       = {
    "{" ~ PUSH(paired_tag_names) ~ "}" ~ (txt | tag)+ ~ "{\\" ~ POP ~ "}"
}

tag = { (single_tag | paired_tag) }

topic  = @{ "{topic=" ~ ASCII_ALPHANUMERIC+ ~ "}" }
weight = @{ "{weight=" ~ ASCII_DIGIT+ ~ "}" }

format_tag = @{ "{" ~ "/"? ~ format_tag_name ~ "}" }

ok = { "{ok}" }

escapes = @{ "\\" ~ ("n" | "s" | "/" | "#") }
